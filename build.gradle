plugins {
    id 'groovy'
    id 'org.jetbrains.kotlin.jvm' version '1.2.41'
    id 'java-gradle-plugin'
}

apply from: 'gradle-mvn-push.gradle'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    jcenter()
    google()
}

dependencies {
    compile 'com.android.tools.build:gradle:3.0.1'
    compile('com.google.apis:google-api-services-androidpublisher:v2-rev77-1.23.0') {
        exclude group: 'com.google.guava', module: 'guava-jdk5'
    }
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.2.41'

    testImplementation "org.jetbrains.kotlin:kotlin-test:1.2.41"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.6.2'
    testImplementation 'org.mockito:mockito-core:1.10.19'
}

gradlePlugin {
    plugins {
        play {
            id = 'com.github.triplet.play'
            implementationClass = 'de.triplet.gradle.play.PlayPublisherPlugin'
        }
    }
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {

                pom.project {
                    developers {
                        developer {
                            id 'bhurling'
                            name 'Bjoern Hurling'
                        }
                        developer {
                            id 'ChristianBecker'
                            name 'Christian Becker'
                        }
                        developer {
                            id 'gtcompscientist'
                            name 'Charles Anderson'
                        }
                    }
                }

                pom.whenConfigured { pom ->
                    pom.dependencies = pom.dependencies.findAll { dep ->
                        dep.scope != 'test'
                    }
                }
            }
        }
    }
}

// Compile Kotlin then Groovy
afterEvaluate {
    compileKotlin.dependsOn.remove(compileJava)
    compileGroovy.dependsOn compileKotlin
    compileGroovy.classpath += files(compileKotlin.destinationDir)
    classes.dependsOn compileGroovy
}
